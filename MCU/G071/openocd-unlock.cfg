# unlock script for openocd for STM32G071 MCUs

source [find interface/stlink.cfg]
source [find target/stm32g0x.cfg]
init

set FLASH_CR      0x40022014
set FLASH_SR      0x40022010
set FLASH_KEYR    0x40022008
set FLASH_OPTKEYR 0x4002200C
set FLASH_OPTR    0x40022020

set FLASH_PCROPA_START 0x40022024
set FLASH_PCROPA_END   0x40022028

set FLASH_WPA 0x4002202C
set FLASH_WPB 0x40022030

set FLASH_PCROPB_START 0x40022034
set FLASH_PCROPB_END   0x40022038

set KEY1 0x45670123
set KEY2 0xCDEF89AB
set OPT_KEY1 0x08192A3B
set OPT_KEY2 0x4C5D6E7F

if { ![info exists BOOTLOADER] } {
   # default for convenience for testing
   set BOOTLOADER "bootloaders/AM32_G071_BOOTLOADER_PA2_V12.bin"
}

echo "Using bootloader $BOOTLOADER"


# function to wait for some bits to clear
proc wait_till_clear { register bits } {
    # always wait 10ms
    sleep 10
    set v [mrw $register]
    while {[expr {$v & $bits}] != 0} {
        #echo "reg [format 0x%08x $register] [format 0x%08x $v] [format 0x%08x $bits]"
        sleep 1
        set v [mrw $register]
    }
}

proc get_protections { } {
    set OPTR [mrw 0x40022020]
    echo "OPTR [format 0x%08x $OPTR]"

    set OPT_RDP [expr {($OPTR & 0xFF)}]
    echo "OPT_RDP [format 0x%02x $OPT_RDP]"

    set PCROP_START [mrw 0x40022024]
    set PCROP_END [mrw 0x40022028]
    echo "PRROP [format 0x%08x $PCROP_START] [format 0x%08x $PCROP_END]"

    set WP_START [mrw 0x4002202C]
    set WP_END [mrw 0x40022030]
    echo "WP [format 0x%08x $WP_START] [format 0x%08x $WP_END]"

    if { $OPT_RDP != 0xAA } {
        echo "Flash read out protection enabled"
        return 1
    }
    return 1
}

proc show_register { register } {
     set v [mrw $register]
     echo "reg [format 0x%08x $register] [format 0x%08x $v]"
}

if { [get_protections] } {
    echo "FLASH_CR [format 0x%08x [mrw $FLASH_CR]]"

    echo "unlocking flash"
    mww $FLASH_KEYR $KEY1
    mww $FLASH_KEYR $KEY2

    echo "FLASH_CR [format 0x%08x [mrw $FLASH_CR]]"

    echo "unlocking option"
    mww $FLASH_OPTKEYR $OPT_KEY1
    mww $FLASH_OPTKEYR $OPT_KEY2

    echo "FLASH_CR [format 0x%08x [mrw $FLASH_CR]]"

    set OPTR [mrw $FLASH_OPTR]
    # set the options we want
    set NEW_OPTR 0x7d4fe0aa
    echo "NEW_OPTR [format 0x%08x $NEW_OPTR]"

    echo "writing OPTR"
    mww $FLASH_OPTR $NEW_OPTR

    echo "disabling protections"
    mww $FLASH_PCROPA_START 1
    mww $FLASH_PCROPA_END 0x80000000
    mww $FLASH_WPA 0x003f003f
    mww $FLASH_WPB 0x003f003f
    mww $FLASH_PCROPB_START 1
    mww $FLASH_PCROPB_END 0

    echo "send OPTSTRT"
    mww $FLASH_CR 0x20000
    # wait for BSY1 clear
    wait_till_clear $FLASH_SR 0x10000

    echo "option reload"
    mww $FLASH_CR 0x08000000
    wait_till_clear $FLASH_CR 0x08000000
    reset

    show_register $FLASH_OPTR
    show_register $FLASH_PCROPA_START
    show_register $FLASH_PCROPA_END
    show_register $FLASH_WPA
    show_register $FLASH_WPB
    show_register $FLASH_PCROPB_START
    show_register $FLASH_PCROPB_END
}

halt

echo "erase 2 bootloader sectors and first fw sector"
flash erase_sector 0 0 2
wait_till_clear $FLASH_SR 0x10000

echo "Load bootloader"
flash write_bank 0 $BOOTLOADER

echo "verify bootloader"
flash verify_bank 0 $BOOTLOADER

echo "Success!"
exit
