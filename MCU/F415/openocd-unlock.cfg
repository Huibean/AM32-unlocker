# unlock for openocd for AT32F415 MCUs
source [find interface/stlink.cfg]
source [find target/at32f415xx.cfg]

init
reset
halt

if { ![info exists BOOTLOADER] } {
   # default for convenience for testing
   set BOOTLOADER "bootloaders/AM32_F415_BOOTLOADER_PA2_V12.bin"
}

echo "Using bootloader $BOOTLOADER"


proc show_register { register } {
     set v [mrw $register]
     echo "reg [format 0x%08x $register] [format 0x%08x $v]"
}

proc set_bits { register bits } {
    set v [mrw $register]
    set v [expr {($v | $bits)}]
    mww $register $v
}

# function to wait for some bits to clear
proc wait_till_clear { register bits } {
    # always wait 10ms
    sleep 10
    set v [mrw $register]
    while {[expr {$v & $bits}] != 0} {
        #echo "reg [format 0x%08x $register] [format 0x%08x $v] [format 0x%08x $bits]"
        sleep 1
        set v [mrw $register]
    }
}


set FLASH_UNLOCK 0x40022004
set FLASH_USD_UNLOCK 0x40022008
set FLASH_STS 0x4002200C
set FLASH_CTRL 0x40022010

set USD_FAP 0x1FFFF800
set EPP0 0x1FFFF808
set EPP1 0x1FFFF80A
set EPP2 0x1FFFF80C
set EPP3 0x1FFFF80E

set KEY1 0x45670123
set KEY2 0xCDEF89AB

show_register $FLASH_CTRL

echo "unlock flash"
mww $FLASH_UNLOCK $KEY1
mww $FLASH_UNLOCK $KEY2

echo "unlock USD"
mww $FLASH_USD_UNLOCK $KEY1
mww $FLASH_USD_UNLOCK $KEY2

show_register $FLASH_CTRL

echo "erase USD"
wait_till_clear $FLASH_STS 1
set_bits $FLASH_CTRL 0x60
wait_till_clear $FLASH_STS 1
show_register $FLASH_STS

echo "Clear protection"
set_bits $FLASH_CTRL 0x10000

echo "program USD"
set_bits $FLASH_CTRL 0x10

echo "set FAP disable"
mwh $USD_FAP 0xa5

echo "disable EPPS"
set_bits $FLASH_CTRL 0x10
mww $EPP0 0x00ff00ff
set_bits $FLASH_CTRL 0x10
mwh $EPP2 0x00ff00ff

wait_till_clear $FLASH_STS 1
show_register $FLASH_STS

at32f4xx disable_access_protection 0

sleep 100

init
reset

show_register $USD_FAP
show_register $EPP0
show_register $EPP1
show_register $EPP2
show_register $EPP3

init
reset
halt

echo "erase 2 bootloader sectors and first fw sector"
flash erase_sector 0 0 4

echo "Load bootloader"
flash write_bank 0 $BOOTLOADER

echo "verify bootloader"
flash verify_bank 0 $BOOTLOADER

echo "Success!"
exit
